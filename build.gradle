buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

// Read properties file
ext {
    configFile = file "build.properties"

    configFile.withReader {
        // Load config.  It shall from now be referenced as simply config or project.config
        def prop = new Properties()
        prop.load(it)
        config = new ConfigSlurper().parse prop
    }
}

version = config.mod.version
group= "eu.thog92.isbrhcore"
archivesBaseName = "isbrhcore"

if (System.env.BUILD_NUMBER != null) version = "$version.$System.env.BUILD_NUMBER"

minecraft {
    version = "${config.minecraft.version}-${config.forge.version}-${config.minecraft.version}"
    runDir = "run"
    mappings = "snapshot_nodoc_20150113"
}

processResources
{
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

jar {
    manifest {
        attributes("FMLCorePlugin": "eu.thog92.isbrh.coremod.ISBRHILoadingPlugin", "FMLAT": "isbrhcore_at.cfg", "FMLCorePluginContainsFMLMod": "true")
    }
}

runClient {
    jvmArgs '-Dfml.coreMods.load=eu.thog92.isbrh.coremod.ISBRHILoadingPlugin -isbrhCore.enableExemple=true'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives sourcesJar
    archives devJar
}
